Skillbar_Handler:
  type: world
  debug: false
  events:
    on player scrolls their hotbar:
    - if <player.gamemode.id> == 2 && <context.new_slot> <= 6 {
      - determine passively cancelled
      - if <player.has_flag[Class]> && <player.flag[Class]> !== Citizen {
        - if <context.previous_slot> <= 6 {
          - adjust <player> item_slot:7
        }
        - if <player.inventory.slot[<context.new_slot>].scriptname> == Unassigned_Skill {
          - narrate "<&7>This skill has not been assigned yet!"
        }
        else {
          - define Skill <player.inventory.slot[<context.new_slot>].scriptname.to_titlecase>
          - define Skill_Slot <context.new_slot>
          - inject Cast_Sequence
        }
      }
    }
    on player clicks in inventory:
    - if <player.gamemode.id> == 2 && <context.slot> <= 6 {
      - determine passively cancelled
    }

Cast_Sequence:
  type: task
  debug: false
  speed: 0
  script:
    - if <player.has_flag[Silence_Effect]> {
      - narrate "<&a><&l>[Skill] <&7>You are currently silenced for another <&f><player.flag[Silence_Effect].expiration.in_seconds.round_to[1]>s<&7>!"
      - queue clear
    }
    - define Skill_Cooldown <def[Skill]>_Cooldown
    - define Class_Skill_Config s@<player.flag[Class]>_Skill_Config
    - inject <def[Skill]>_Cast
    - flag player <def[Skill_Cooldown]> duration:<def[Class_Skill_Config].yaml_key[<def[Skill_Cooldown]>].as_int||4>
    - define Message "<def[Class_Skill_Config].yaml_key[<def[Skill]>_Message]||<&a><&l>[Skill] <&f><player.name> <&7>has used <&f><def[Skill]><&7>!>"
    - narrate "<parse:<def[Message]>>" targets:<player.location.find.players.within[25]>
    - repeat <def[Class_Skill_Config].yaml_key[<def[Skill_Cooldown]>].as_int||10> {
      - adjust <player.inventory.slot[<def[Skill_Slot]>]> quantity:<player.flag[<def[Skill_Cooldown]>].expiration.in_seconds.round> save:Cooldown
      - take slot:<def[Skill_Slot]>
      - inventory set origin:<entry[Cooldown].result> slot:<def[Skill_Slot]>
      - wait 1s
    }

Unassigned_Skill:
  type: item
  material: pumpkin_seeds
  display name: <&7>Unassigned
  lore:
  - <&f>Unassigned Skill